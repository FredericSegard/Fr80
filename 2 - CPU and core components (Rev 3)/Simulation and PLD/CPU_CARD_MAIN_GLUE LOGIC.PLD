Name      CPU_card_main_glue_logic ;
PartNo    ATF22V10C ;
Date      21/8/2023 ;
Revision  1 ;
Designer  Frederic Segard ;
Company   @microhobbyist ;
Assembly  None ;
Location  ;
Device    g22v10 ;

/* *************** INPUT PINS *****************************/
PIN 1   = RESET    ; /* Reset (int. SR R)                 */
PIN 2   = MREQ     ; /* Memory request                    */
PIN 3   = IORQ     ; /* I/O request                       */
PIN 4   = M1       ; /* M1 cycle                          */
PIN 5   = WR       ; /* Write                             */
PIN 6   = RD       ; /* Read                              */
PIN 7   = A2       ; /* Address line 2                    */
PIN 8   = A3       ; /* Address line 3                    */
PIN 9   = A4       ; /* Address line 4                    */
PIN 10  = A5       ; /* Address line 5                    */
PIN 11  = A6       ; /* Address line 6                    */
PIN 13  = A7       ; /* Address line 7                    */


/* *************** OUTPUT PINS ****************************/
PIN 23  = MODE2OE  ; /* Auto load interrupt vector        */
PIN 22  = INTRD    ; /* Read interrut priority register   */
PIN 21  = BANKWR   ; /* Write BANK register               */
PIN 20  = BANKRD   ; /* Read BANK register (also ROM_DIS) */
PIN 19  = SIOCS    ; /* SIO chip select                   */
PIN 18  = CTCCS    ; /* CTC chip select                   */
PIN 17  = RAMOE    ; /* RAM output enable                 */
PIN 16  = ROMCS    ; /* ROM chip select                   */
PIN 15  = RAMSEL   ; /* RAM select (int. SR)              */
PIN 14  = ROMSEL   ; /* ROM select (int. SR)              */ 


/* *************** EQUATIONS ****************************/
FIELD ADDRESS = [A7..2] ;

 RAMSEL  =  !(RESET   &  ROMSEL) ;
 ROMSEL  =  !(BANKRD  &  RAMSEL) ;
 ROMCS   =  ROMSEL  #  MREQ ;
 RAMOE   =  RAMSEL  #  RD ;

 MODE2OE =  IORQ # M1 ;

 BANKWR =  ADDRESS:[88..8B] & M1 & !IORQ & !WR ;
!BANKRD =  ADDRESS:[88..8B] & M1 & !IORQ & !RD ; /* ROM disable */
!INTRD  =  ADDRESS:[3C..3F] & M1 & !IORQ & !RD ;
!SIOCS  =  ADDRESS:[00..03] & M1 & !IORQ ;
!CTCCS  =  ADDRESS:[04..07] & M1 & !IORQ ;
